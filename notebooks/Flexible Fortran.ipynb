{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Flexibility\n",
    "\n",
    "In this section we'll be looking at ways of extending the functionality of Modern Fortran, using features widely used in other programming languages such as C++, but which have no equivalence in legacy Fortran."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Derived Types\n",
    "\n",
    "We've seen examples of _Intrinsic Types_, the variable types that can be used in Fortran code. We can imagine the situation where we have a very large number of variables decribing the property of an object, any of which are meaningless outside the context of the whole. Take a car, for example. We let if have `colour`, `doors`, `seats`, `age`, and a number of other attributes, but all of these refer to a single object, _the car_. To write any function or subroutine that performs an operation on _the car_, one must pass all its properties separately (and out of context) and return them. Particularly if there a very large number of attributes, this can make code unreadable or potentially even breach Fortran's limit on the permitted number of arguments.\n",
    "\n",
    "Fortunately, there is an alternative. A _Derived Type_ is a user-defined compound variable consisting of a number of intrinsic types, and can be used to package related variables, giving them context. An example of a very simple derived type follows:\n",
    "\n",
    "```fortran\n",
    "Type <name>\n",
    "  Type    :: var_name\n",
    "  Type    :: var_name\n",
    "End type <name>\n",
    "```\n",
    "This is simply a definition. We'd then have to declare an object of this type like so:\n",
    "\n",
    "```fortran\n",
    "Type (<name>)  :: my_object\n",
    "```\n",
    "\n",
    "We can also have more than one object of this type, or even an array of them.\n",
    "\n",
    "For our car, we could define a type as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%num_images: 1\n",
    "Program kind_demo\n",
    "  Implicit none\n",
    "\n",
    "  ! Define a derived type describing a car\n",
    "  Type car\n",
    "    Character(15)  :: colour\n",
    "    Character(15)  :: name\n",
    "    Integer        :: doors\n",
    "    Integer        :: seats\n",
    "    Integer        :: age\n",
    "  End type car\n",
    "  \n",
    "  ! Declare an array of 'cars'\n",
    "  Type (car)       :: some_cars(2)\n",
    "  \n",
    "  ! Declare a loop counter\n",
    "  Integer          :: x\n",
    "  \n",
    "  ! Populate the first car one element at a time\n",
    "  some_cars(1)%colour = 'Red'\n",
    "  some_cars(1)%name   = 'My Car'\n",
    "  some_cars(1)%doors  = 5\n",
    "  some_cars(1)%seats  = 4\n",
    "  some_cars(1)%age    = 3\n",
    "  \n",
    "  ! Populate the second car all at once\n",
    "  some_cars(2) = car(colour='Blue', name='Some Other Car', doors=7, seats=2, age=5)\n",
    "  \n",
    "  ! Print out the details of each car\n",
    "  do x = 1, 2\n",
    "    print *, some_cars(x)%name\n",
    "    print *, some_cars(x)%colour\n",
    "    print \"(i2)\", some_cars(x)%doors\n",
    "    print \"(i2)\", some_cars(x)%seats\n",
    "    print \"(i2)\", some_cars(x)%age\n",
    "  end do\n",
    "  \n",
    "End Program"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note that the syntax for accessing an element within a derived type is to prefix the name of the element with the name of the containing object separated by a `%` sign, i.e. `some_cars(1)%age`.\n",
    "\n",
    "This `car` object can then be passed to functions, assuming that both program units are aware of the definition of the derived type. The ideal way to do this would be to place it in the header of a _module_ and `use` that module in any units that require the definition. An example of this is shown below:\n",
    "\n",
    "| Program  | Directory              | Purpose                                                    |\n",
    "| -------- | ---------------------- | ---------------------------------------------------------- |\n",
    "| **cars** | `src/derived/example1` | Demonstrate how derived types can be used in modules       |\n",
    "\n",
    "Note that because we are passing objects of type `car` into a subroutine with `intent(in)` we can't just update that object and return it. We have to make a working copy and update that. This is the case with all variables passed with this specific intent and means we are sacrificing some speed and memory for better quality control."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Exercise 1 - _Derived Types_\n",
    "\n",
    " * Create your own derived type and 'process' it as in the above example."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Dynamic Memory\n",
    "\n",
    "In the examples so far we've specified the size of a required array in the program code. If we need an array of 15 integers we have to write it like this:\n",
    "\n",
    "```fortran\n",
    "Integer  :: my_array(15)\n",
    "```\n",
    "\n",
    "But what if we don't know how large an array we need? Let's say that we are reading in entries from a file and don't know ahead of time how many to expect. We could set a fixed upper limit (15 in this case) and risk either wasting memory or running out, but what we can't do is leave the length undefined and assume we increase it according to demand as we can in language like Python. We can't, for example, do this:\n",
    "\n",
    "```fortran\n",
    "Integer  :: my_array(*)\n",
    "```\n",
    "\n",
    "This is because the Fortran compiler needs to know the size of these variables at compile time in order to be able to allocate the space for them.\n",
    "\n",
    "_However_, although we can't declare standard arrays of undefined size, we can use the `allocatable` attribute, to defer the choice of size from _compile time_ to _run time_. We could then test the number of elements we need to read it, perhaps by querying the size of a file, and then allocating enough memory to hold that data.\n",
    "\n",
    "An two-dimensional 'allocatable' array is declared as follows:\n",
    "\n",
    "```fortran\n",
    "Integer, allocatable  :: my_array(:, :)\n",
    "```\n",
    "\n",
    "At this point the array has undefined size and can't be used. Once we know the amount of data that the array needs to hold we can allocate that space:\n",
    "\n",
    "```fortran\n",
    "var_x = 20\n",
    "var_y = 15\n",
    "\n",
    "! Allocate space to the reserved 'my_array'\n",
    "allocate(my_array(var_x, var_y)\n",
    "```\n",
    "\n",
    "A working demonstration follows. Since allocating an array twice without first `deallocating` it is not allowed, we can use the `allocated()` intrinsic to check its status before allocating it some memory."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%num_images: 1\n",
    "Program dynamic_demo\n",
    "  Implicit none\n",
    "  \n",
    "  ! Declare an allocatable array of currently undefined size\n",
    "  Integer, allocatable :: my_array(:, :)\n",
    "  \n",
    "  ! Decare variables to hold the size of the array dimensions and indices\n",
    "  Integer              :: x_size, y_size\n",
    "  Integer              :: x, y\n",
    "  \n",
    "  ! Imagine we have determined the required extent of our array\n",
    "  x_size = 15\n",
    "  y_size = 7\n",
    "  \n",
    "  ! If my_array has not yet been allocated then assign it some storage space\n",
    "  if (.not. allocated(my_array)) then\n",
    "    allocate(my_array(x_size, y_size))\n",
    "  else\n",
    "    print *, 'my_array already has been allocated'\n",
    "  end if  \n",
    "  \n",
    "  ! Check the size of each dimension\n",
    "  print *, 'Size of first dimension:  ', size(my_array, 1)\n",
    "  print *, 'Size of second dimension: ', size(my_array, 2)\n",
    "  \n",
    "  ! Try allocating it again. Fortran doesn't allow this.\n",
    "  if (.not. allocated(my_array)) then\n",
    "    allocate(my_array(x_size, y_size))\n",
    "  else\n",
    "    print *, 'my_array already has been allocated'\n",
    "  end if  \n",
    "\n",
    "End Program"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Once allocated, _allocatable arrays_ can be treated exactly like standard arrays. They have _interfaces_ automatically generated when used in a module, and they can even be returned from subroutines and functions, as shown in the following example:\n",
    "\n",
    "| Program     | Directory              | Purpose                                                    |\n",
    "| ----------- | ---------------------- | ---------------------------------------------------------- |\n",
    "| **dynamic** | `src/dynamic/example1` | Example of how dynamic arrays can be passed between units  |\n",
    "\n",
    "In this case the subroutine doesn't need to know what size of array it has received. All features of the array can be determined by using intrinsic functions, so we don't have to modify the code depending on the size of the array received. Note that even though the dynamic array is allocated in the subroutine, it stays in scope (remains allocated) along with its values, when returned back to the main program.\n",
    "\n",
    "### Excercise 2 - _Dynamic Memory_\n",
    "\n",
    " * The scope of dynamic arrays in modules is a little counterintuitive. We can declare a dynamic array in the header (before `contains`) and allocate it in the subroutine, as though we were treating the module like a _Class_. If we do that the the variable remains in scope as long as the module is loaded (i.e. forever!). Try doing this and calling `proc` repeatedly. How would you solve this problem?"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Parallelising your Code\n",
    "\n",
    "The topic of parallelising code is a very large one and will not be dealt with in great depth here. Fortran compilers generally support _OpenMP_, and there are a large number of _Message Passing_ libraries available, such as a Intel MPI and the open source _OpenMPI_ (not to be confused with OpenMP).\n",
    "\n",
    "In addition to these general protocols, Fortran offers a specific method of parallel programming called **CoArrays**. It has similarities to _MPI_ but automates a lot of the work involved in MPI programming and provides functions for dealing with parallelised data that would be more familiar to the Fortran programmer.\n",
    "\n",
    "### CoArray Programming\n",
    "\n",
    "A CoArray Fortran program consists of a number of identical images, or copies of the program, running simultaneously and independently. Like _MPI_, it is possible to query the numerical ID of the image to determine one from another, and explicitly pass messages from one to another (point-to-point) or across all processes (collective). In this way, a large dataset can be split up into chunks, with each chunk belonging to a separate process. Unlike MPI, CoArray allows each processes to directly access the data of another process without explicit message passing.\n",
    "\n",
    "A simple example, demonstrating the MPI-like ability to query the ID of a process is shown below. This uses the Coarray functions `this_image()` and `num_images()` to identify an individual process within the total pool. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%num_images: 4\n",
    "Program query_id\n",
    "  Implicit none\n",
    "\n",
    "  ! Print the image or process ID, and the total number of images\n",
    "  print *, 'I am process number: ', this_image(), ' out of ', num_images()\n",
    "End program"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Arrays\n",
    "\n",
    "The heart of CoArrays is the concept that each process has it's own copy of an array, but can also access data belonging to the array of the same name from any other process. Accessing the local copy of an array is identical to standard Fortran syntax:\n",
    "\n",
    "```fortran\n",
    "  my_array(2) = 100\n",
    "```\n",
    "\n",
    "This simply assigns the value 100 to element 2 of a one dimensional array named `my_array`.\n",
    "\n",
    "Accessing a copy of the array belonging to another process is achieved by including the ID of that process in `[]` brackets:\n",
    "\n",
    "```fortran\n",
    "  my_array[2](2) = 120\n",
    "```\n",
    "\n",
    "Assuming that the process ID is 1, then this operation will assign the value of 120 to the copy of `my_array` belonging to process 2. Behind the scenes this is achieved by an MPI _point-to-point_ communication.\n",
    "\n",
    "A functional example of this using two processes is shown below:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%num_images: 2\n",
    "Program coarray\n",
    "  Implicit none\n",
    "\n",
    "  ! Declare a one-dimensional 'coarray' with four elements. The `[*]` is known as the co-index\n",
    "  ! and should generally not be changed. It is assigned with the program is run and is equal\n",
    "  ! to the number of processes.\n",
    "  Integer :: my_array(4)[*]\n",
    "  \n",
    "  ! Array index counter\n",
    "  Integer :: x\n",
    "  \n",
    "  ! Each process can assign values to its own copy of the array.\n",
    "  do x = 1, ubound(my_array, 1)\n",
    "    my_array(x) = this_image() * 100 + x\n",
    "  end do\n",
    "  \n",
    "  ! Print out the coarray for each image\n",
    "  print \"(A6x, i1, A7x, 4i6)\", 'image:', this_image(), 'array:', my_array\n",
    "  \n",
    "  ! Image 1 can change values in Image 2's copy of my_array using the [x] image specifier\n",
    "  if (this_image() == 1) then\n",
    "    my_array(3)[2] = 555\n",
    "  end if\n",
    "  \n",
    "  ! Since images run indendently we need to make sure the above operation completed\n",
    "  ! before we try and test the results\n",
    "  sync all\n",
    "  \n",
    "  ! Print out the coarrays again to check this worked\n",
    "  print \"(A6x, i1, A7x, 4i6)\", 'image:', this_image(), 'array:', my_array\n",
    "\n",
    "End program"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Exercise 3 - _Arrays_\n",
    "\n",
    " * What happens if you remove the `sync` line from the above example. Try running the code multiple times with sync removed and see if you can explain the behaviour. Remember, images will run independently unless communication takes place between them.\n",
    " * Can you change the example so that image 2 _also_ changes the values in image 1?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Coarray Fortran",
   "language": "Fortran",
   "name": "coarray-fortran"
  },
  "language_info": {
   "file_extension": ".f90",
   "mimetype": "text/plain",
   "name": "fortran"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
